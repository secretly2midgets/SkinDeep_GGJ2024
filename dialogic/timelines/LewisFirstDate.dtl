join LewisBolson 2
set {QuestionCounter} = 0
set {DateNumber} += 1
set {AffectionLewis} += 1
LewisBolson: Uh.. Hi. My name's Lewis. It's nice to meet you. I don't usually come to places like this.
label InitialChoices
set {QuestionCounter} += 1
LewisBolson: I've never been on a date before... this is pretty fun.
- Ask questions
	jump FirstDate/Questions
- Flirt
	jump Flirtations
- Use shotgun
	LewisBolson (LewisHill): Holy SHIT, where'd you get-
	if {Skinwalker} == "Lewis":
		LewisBolson: You figured it out quick... and you looked so scrumptious...
		leave LewisBolson
		jump QuickEnd/
	else:
		leave LewisBolson
		join Narrator 2
		Narrator (default): It's over in a second. Lewis' head is instantly turned to pulp all over the tables behind him. You are arrested and found guilty in a trial lasting a record 3 minutes to reach a verdict.
		jump JailEnding/
- Second Date? [if {QuestionCounter} >= 5]
	jump 2ndDate?
label Questions
LewisBolson: Uh.. sure. What do you wanna know?
- How do you feel about family?
	if {Skinwalker} == "Lewis":
		LewisBolson: My family is dead.
		set {AffectionLewis} += 1
	else:
		LewisBolson: I mean, my family’s okay?
		Narrator: He pauses, then his eyes widen in realization.
		LewisBolson: Oh, you mean like... you and me... I’d like to have one.
	jump InitialChoices
- Where do you work?
	LewisBolson: I work at LunaTech. It’s pretty fun, but I wish people knew how to unplug their computers instead of asking me.
	jump InitialChoices
- Your life is predetermined, nothing is in your control. This conversation is scheduled to happen now. How do you break the cycle?
	Narrator: Lewis pauses, and stares up at the ceiling.
	LewisBolson: I would raid CERN, and stick my head in a Hadron collider.
	set {AffectionLewis} += 1
	jump InitialChoices
label Flirtations
LewisBolson: Uhm... you have beautiful eyes, you know.
- *Stare at him*
	Narrator: You gaze deeply into Lewis’ eyes. He stares back.
	Narrator: In the distance, you hear a witch’s cackle.
	Narrator: The curtains writhe.
	set {AffectionLewis} += 1
	jump InitialChoices
- Are you my program crashing? Because you're making me scream.
	if {Skinwalker} == "Lewis":
		LewisBolson: What?
		set {AffectionLewis} += 1
	else:
		LewisBolson: Invalid set index 'end_node' (on base\: 'Nil') with value of type 'HBoxContainer (BranchEnd.gd)'
	jump InitialChoices
- *Stick your entire fist in your mouth*
	if {Skinwalker} == "Lewis":
		LewisBolson: Good. Now pull it out.
		LewisBolson: Now stick it back in.
		LewisBolson: Now back out.
		LewisBolson: And in.
		LewisBolson: Good.
		set {AffectionLewis} += 1
	else:
		LewisBolson: Holy. Fucking. Shit. That's incredible. What else can you fit in there? Well, I mean, not like that. Unless, yenno... you mean it that way.
		set {AffectionLewis} += 1
	jump InitialChoices
label 2ndDate?
if {AffectionLewis} >= {AffectionThreshold.First}:
	LewisBolson: So.. most people leave by this point... would you wanna see me again?
	- Yes
		set {AtLeastOneDateSuccess} = "true"
		LewisBolson: Quest complete! Second date acquired! See you next week!
	- No
		LewisBolson: Oh... ok.
		set {AffectionLewis} = -10
		set {HowManyLeft} -= 1
else:
	LewisBolson: Uh.. Not thanks. You're a bit intense for me.
	set {AffectionLewis} = -10
	set {HowManyLeft} -= 1
Narrator: Lewis checks his phone, and tells you his ride is here. He thanks you for the date, and offers to pay for your coffee.
leave LewisBolson
jump DateOneInterstitial/
